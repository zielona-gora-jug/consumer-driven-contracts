buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:1.2.1.RELEASE"
    }
}

subprojects {

    group 'pl.jerckov.cdc'
    version '1.0.0'

    apply plugin: 'java'
    apply plugin: 'groovy'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        compile 'org.springframework.boot:spring-boot-starter-web:1.5.9.RELEASE'
    }
}

project(':ui') {

    dependencies {
        testCompile 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner:1.2.1.RELEASE'
        testCompile 'org.assertj:assertj-core:3.8.0'
        testCompile 'pl.jerckov.cdc:user-service:+:stubs'
    }

    test.dependsOn(":user-service:publishToMavenLocal")
}

project(':crm') {

    dependencies {
        testCompile 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner:1.2.1.RELEASE'
        testCompile 'org.assertj:assertj-core:3.8.0'
        testCompile 'pl.jerckov.cdc:user-service:+:stubs'
    }

    test.dependsOn(":user-service:publishToMavenLocal")
}

project(':user-service') {

    apply plugin: 'maven-publish'
    apply plugin: 'spring-cloud-contract'

    sourceSets {
        test {
            groovy {
                srcDir './build/generated-test-sources'
            }
        }
    }

    dependencies {
        testCompile 'org.codehaus.groovy:groovy-all:2.4.13'
        testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
        testCompile 'org.spockframework:spock-spring:1.1-groovy-2.4'
        testCompile 'org.springframework.cloud:spring-cloud-starter-contract-verifier:1.2.1.RELEASE'
    }

    contracts {
        targetFramework = 'Spock'
        contractsDslDir = file('./cdc-contracts')
        generatedTestSourcesDir = file('./build/generated-test-sources')
        baseClassForTests = 'pl.jug.cdc.server.ServerSpec'
        basePackageForTests = 'pl.jug.cdc.server.contracts'
    }
}

